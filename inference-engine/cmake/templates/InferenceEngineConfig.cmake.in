# Copyright (C) 2018-2021 Intel Corporation
# SPDX-License-Identifier: Apache-2.0
#
#
# Inference Engine cmake config
# ------
#
# This will define the following variables:
#
#   InferenceEngine_FOUND        - True if the system has the Inference Engine library
#   InferenceEngine_INCLUDE_DIRS - Inference Engine include directories
#   InferenceEngine_LIBRARIES    - Inference Engine libraries
#
# and the following imported targets:
#
#   IE::inference_engine            - The Inference Engine library
#   IE::inference_engine_c_api      - The Inference Engine C API library
#

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(THREADING "@THREADING@")
if(THREADING STREQUAL "TBB" OR THREADING STREQUAL "TBB_AUTO")
    set_and_check(_tbb_cmake "@PACKAGE_IE_TBB_DIR@")
    find_dependency(TBB
                    CONFIG
                    PATHS ${TBBROOT}/cmake
                          ${_tbb_cmake}
                    NO_CMAKE_FIND_ROOT_PATH
                    NO_DEFAULT_PATH)
endif()

set_and_check(_ngraph_cmake "@PACKAGE_IE_NGRAPH_CMAKE_PATH@")
find_dependency(ngraph
                CONFIG
                PATHS ${_ngraph_cmake}
                NO_CMAKE_FIND_ROOT_PATH
                NO_DEFAULT_PATH)

function(_ie_target_no_deprecation_error)
    if(NOT MSVC)
        if(CMAKE_CXX_COMPILER_ID STREQUAL "Intel")
            set_target_properties(${ARNV} PROPERTIES
                INTERFACE_COMPILE_OPTIONS "-diag-warning=1786")
        else()
            set_target_properties(${ARNV} PROPERTIES
                INTERFACE_COMPILE_OPTIONS "-Wno-error=deprecated-declarations")
        endif()
    endif()
endfunction()

if(TARGET inference_engine)
    set(InferenceEngine_LIBRARIES inference_engine inference_engine_c_api)
    _ie_target_no_deprecation_error(${InferenceEngine_LIBRARIES})
else()
    include("${CMAKE_CURRENT_LIST_DIR}/InferenceEngineTargets.cmake")
    set(InferenceEngine_LIBRARIES IE::inference_engine IE::inference_engine_c_api)
endif()

set_and_check(InferenceEngine_INCLUDE_DIRS "@PACKAGE_IE_CPACK_IE_INCLUDE_DIR@")

# TODO: IE options for case if TARGET inference_engine
# TODO: do we need to include ie_parallel.cmake?

check_required_components(InferenceEngine)
