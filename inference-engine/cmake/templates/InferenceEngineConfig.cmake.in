# Copyright (C) 2018-2021 Intel Corporation
# SPDX-License-Identifier: Apache-2.0
#
#
# Inference Engine cmake config
# ------
#
# This will define the following variables:
#
#   InferenceEngine_FOUND        - True if the system has the Inference Engine library
#   InferenceEngine_INCLUDE_DIRS - Inference Engine include directories
#   InferenceEngine_LIBRARIES    - Inference Engine libraries
#
# and the following imported targets:
#
#   IE::inference_engine            - The Inference Engine library
#   IE::inference_engine_c_api      - The Inference Engine C API library
#

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# need to store current PACKAGE_PREFIX_DIR, because it's overwritten by ngraph one
set(IE_PACKAGE_PREFIX_DIR "${PACKAGE_PREFIX_DIR}")

set(THREADING "@THREADING@")
if(THREADING STREQUAL "TBB" OR THREADING STREQUAL "TBB_AUTO")
    set_and_check(_tbb_dir "@PACKAGE_IE_TBB_DIR@")
    find_dependency(TBB
                    COMPONENTS tbb tbbmalloc
                    CONFIG
                    PATHS ${TBBROOT}/cmake
                          ${_tbb_dir}
                    NO_CMAKE_FIND_ROOT_PATH
                    NO_DEFAULT_PATH)
endif()

set_and_check(_ngraph_dir "@PACKAGE_IE_NGRAPH_DIR@")
find_dependency(ngraph
                CONFIG
                PATHS ${_ngraph_dir}
                NO_CMAKE_FIND_ROOT_PATH
                NO_DEFAULT_PATH)

function(_ie_target_no_deprecation_error)
    if(NOT MSVC)
        if(CMAKE_CXX_COMPILER_ID STREQUAL "Intel")
            set(flags "-diag-warning=1786")
        else()
            set(flags "-Wno-error=deprecated-declarations")
        endif()

        set_target_properties(${ARGV} PROPERTIES INTERFACE_COMPILE_OPTIONS ${flags})
    endif()
endfunction()

if(TARGET inference_engine)
    set(InferenceEngine_LIBRARIES inference_engine inference_engine_c_api)
else()
    include("${CMAKE_CURRENT_LIST_DIR}/InferenceEngineTargets.cmake")
    set(InferenceEngine_LIBRARIES IE::inference_engine IE::inference_engine_c_api)
    _ie_target_no_deprecation_error(${InferenceEngine_LIBRARIES})
endif()

# restore PACKAGE_PREFIX_DIR
set(PACKAGE_PREFIX_DIR ${IE_PACKAGE_PREFIX_DIR})

set_and_check(InferenceEngine_INCLUDE_DIRS "@PACKAGE_IE_INCLUDE_DIR@")

check_required_components(InferenceEngine)
