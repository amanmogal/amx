# ******************************************************************************
# Copyright 2017-2021 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ******************************************************************************
#
#
# FindNGraph
# ------
#
# This script defines the following variables and imported targets:
#
#   ngraph::ngraph                         - nGraph core target
#   ngraph_FOUND                           - True if the system has the nGraph library
#   NGRAPH_LIBRARIES                       - nGraph libraries
#
# Frontends:
#
#   ngraph::frontend_manager               - nGraph frontend manager
#
#   ngraph_onnx_ngraph_frontend_FOUND      - True if the system has onnx_ngraph_frontend library
#   ngraph::onnx_ngraph_frontend           - ONNX FrontEnd target (optional)
#
#   ngraph_paddlepaddle_frontend_FOUND     - True if the system has PDPD frontend
#   ngraph::paddlepaddle_ngraph_frontend   - nGraph PDPD frontend (optional)
#

@PACKAGE_INIT@

if(NOT TARGET ngraph)
    include("${CMAKE_CURRENT_LIST_DIR}/ngraphTargets.cmake")

    # create targets with old names for compatibility
    set(core ngraph)
    set(frontendXmanager frontend_manager)
    set(frontendXonnx onnx_ngraph_frontend)
    set(frontendXpaddlepaddle paddlepaddle_ngraph_frontend)

    foreach(target core frontend_manager frontendXonnx frontendXpaddlepaddle)
        string(REPLACE "X" "::" patched_target ${target})
        set(new_target ngraph::${target})
        if(TARGET ngraph::${patched_target} AND NOT TARGET ${new_target})
            add_library(${new_target} INTERFACE IMPORTED)
            set_target_properties(${new_target} PROPERTIES
                INTERFACE_LINK_LIBRARIES ngraph::${patched_target})
        endif()
    endforeach()
endif()

set(ngraph_ngraph_FOUND ON)
set(NGRAPH_LIBRARIES ngraph::ngraph)

set(ngraph_onnx_ngraph_frontend_FOUND @NGRAPH_ONNX_FRONTEND_ENABLE@)

set(ngraph_onnx_importer_FOUND @NGRAPH_ONNX_FRONTEND_ENABLE@)
if(ngraph_onnx_importer_FOUND)
    set(ONNX_IMPORTER_LIBRARIES ngraph::onnx_ngraph_frontend)
    # ngraph::onnx_importer target and variables are deprecated
    # but need to create a dummy target for BW compatibility
    if(NOT TARGET ngraph::onnx_importer)
        add_library(ngraph::onnx_importer INTERFACE IMPORTED)
        set_target_properties(ngraph::onnx_importer PROPERTIES
            INTERFACE_LINK_LIBRARIES ngraph::onnx_ngraph_frontend)
    endif()
endif()

set(ngraph_paddlepaddle_frontend_FOUND @NGRAPH_PDPD_FRONTEND_ENABLE@)

check_required_components(ngraph)
