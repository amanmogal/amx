# ******************************************************************************
# Copyright 2017-2021 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ******************************************************************************
#
#
# FindNGraph
# ------
#
# This script defines the following variables and imported targets:
#
#   ngraph::ngraph                         - nGraph core target
#   ngraph_FOUND                           - True if the system has the nGraph library
#   NGRAPH_LIBRARIES                       - nGraph libraries
#
# Frontends:
#
#   ngraph::frontend_manager               - nGraph frontend manager
#
#   ngraph_onnx_ngraph_frontend_FOUND      - True if the system has onnx_ngraph_frontend library
#   ngraph::onnx_ngraph_frontend           - ONNX FrontEnd target (optional)
#
#   ngraph_paddlepaddle_frontend_FOUND     - True if the system has PDPD frontend
#   ngraph::paddlepaddle_ngraph_frontend   - nGraph PDPD frontend (optional)
#

@PACKAGE_INIT@

function(set_imported_global target)
    get_target_property(IS_IMPORTED_GLOBAL ${target} IMPORTED_GLOBAL)
    if (NOT IS_IMPORTED_GLOBAL)
        set_target_properties(${target} PROPERTIES IMPORTED_GLOBAL TRUE)
    endif()
endfunction()

if(NOT TARGET ngraph)
    include("${CMAKE_CURRENT_LIST_DIR}/ngraphTargets.cmake")
    set_imported_global(ngraph::ngraph)
    set_imported_global(ngraph::frontend_manager)
endif()

set(ngraph_ngraph_FOUND ON)
set(NGRAPH_LIBRARIES ngraph::ngraph)

set(ngraph_onnx_ngraph_frontend_FOUND @NGRAPH_ONNX_FRONTEND_ENABLE@)
if (ngraph_onnx_ngraph_frontend_FOUND AND NOT TARGET onnx_ngraph_frontend AND NOT TARGET ngraph::onnx_importer)
    set_imported_global(ngraph::onnx_ngraph_frontend)
    add_library(ngraph::onnx_importer ALIAS ngraph::onnx_ngraph_frontend)
endif()
set(ngraph_paddlepaddle_frontend_FOUND @NGRAPH_PDPD_FRONTEND_ENABLE@)
if(ngraph_paddlepaddle_frontend_FOUND AND NOT TARGET paddlepaddle_ngraph_frontend)
    set_imported_global(ngraph::paddlepaddle_ngraph_frontend)
endif()

check_required_components(ngraph)
